buildscript {
    repositories {
        mavenCentral()
        maven { url "https://oss.sonatype.org/content/groups/public"}
    }
}

plugins {
    id 'pl.allegro.tech.build.axion-release' version '1.11.0'
}


group 'com.rundeck.plugin'
version '1.0-SNAPSHOT'

apply plugin: 'groovy'
apply plugin: 'java'
apply plugin: 'pl.allegro.tech.build.axion-release'

sourceCompatibility = 1.8

scmVersion {
    ignoreUncommittedChanges = true
    tag {
        prefix = ''
        versionSeparator = ''
        def origDeserialize=deserialize
        //apend .0 to satisfy semver if the tag version is only X.Y
        deserialize = { config, position, tagName ->
            def orig = origDeserialize(config, position, tagName)
            if (orig.split('\\.').length < 3) {
                orig += ".0"
            }
            orig
        }
    }
}

project.version = scmVersion.version


configurations{
    //declare custom pluginLibs configuration to include only libs for this plugin
    pluginLibs

    //declare compile to extend from pluginLibs so it inherits the dependencies
    compile{
        extendsFrom pluginLibs
    }
}


repositories {
    mavenCentral()
}

dependencies {

    compile 'org.codehaus.groovy:groovy-all:2.3.11'
    compile(group:'org.rundeck', name: 'rundeck-core', version: '2.8.2')
    testCompile group: 'junit', name: 'junit', version: '4.12'

    pluginLibs 'com.squareup.retrofit2:retrofit:2.9.0'

    compile "com.google.code.gson:gson:2.8.9"
    pluginLibs (group: 'com.squareup.retrofit2', name: 'converter-gson', version: "2.9.0") {
        exclude group: 'com.google.code.gson', module: 'gson'
    }

}




// task to copy plugin libs to output/lib dir
task copyToLib(type: Copy) {
    into "$buildDir/output/lib"
    from configurations.pluginLibs
}

jar {
    //include contents of output dir
    from "$buildDir/output"
    manifest {
        attributes 'Rundeck-Plugin-Name' : 'PagerDuty Notification'
        attributes 'Rundeck-Plugin-Description' : 'Sends trigger events to your PagerDuty service'
        attributes 'Rundeck-Plugin-Rundeck-Compatibility-Version': '2.8.2+'
        attributes 'Rundeck-Plugin-Tags': 'java,notification,pagerduty'
        attributes 'Rundeck-Plugin-License': 'Apache 2.0'
        attributes 'Rundeck-Plugin-Source-Link': 'https://github.com/rundeck-plugins/pagerduty-notification'
        attributes 'Rundeck-Plugin-Target-Host-Compatibility': 'all'
        attributes 'Rundeck-Plugin-Version': '1.2', 'Rundeck-Plugin-Archive': 'true','Rundeck-Plugin-File-Version': "${project.version}"
        //create space-separated list of pluginLibs
        def libList = configurations.pluginLibs.collect{'lib/'+it.name}.join(' ')
        attributes 'Rundeck-Plugin-Classnames': 'com.rundeck.plugins.PagerDutyNotificationPlugin', 'Rundeck-Plugin-Libs': "${libList}"
    }
}

//set jar task to depend on copyToLib
jar.dependsOn(copyToLib)

